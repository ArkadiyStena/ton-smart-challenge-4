{- 
  TASK 3 - Find and replace binary substring
  Binary string is represented as a cell linked list: string splitted to chunks,
  first chunk stored to the root cell, next one to the cell in ref and so on;
  each cell can have only one ref. 
  Write the method that find and replaces one flags in the binary string
  with another value. Flags and values can be can be of any length, but
  strictly up to 128 bits. The method must replace every flag it finds.
  Flag and the value to be replaced is guaranteed to be greater than 0.
  Lets give a simple example. We have the target flag 101110101 and the value
  to be written 111111111 as inputs, and a linked list of cells, in which the bit
  value of the first cell ends with ...10100001011, and in the ref we have cell that
  starts with 10101000111111...
  The output should be a linked list where the first
  cell ends with ...10100001111, and the second cell starts with 11111000111111...

-}
forall X -> int is_null (X x) asm "ISNULL";
int tuple_length (tuple t) asm "TLEN";
(int) ubits(int v) asm "UBITSIZE";

() recv_internal() {
}

;; testable
(cell) find_and_replace(int flag, int value, cell linked_list) method_id {
    int flag_size = ubits(flag);
    int value_size = ubits(value);
    if (value_size == 0) {
        value_size = 1;
    }
    int cur_str = 0;
    int cur_str_bits = 0;

    slice s = linked_list.begin_parse();
    cell s1 = null();
    if (~ s.slice_refs_empty?()) {
        s1 = s~load_ref();
    }
    (int, int, int) (x, s_bits, s_refs) = s.slice_compute_data_size(1);

    tuple uints = empty_tuple();
    tuple uints_sizes = empty_tuple();
    tuple builders = empty_tuple();
    int bits_left = 1023;
    builder cur_builder = begin_cell();

    int last_update = 0;
    int bits_since_flag = 0;
    int uint_size = 127;
    int p126m1 = (1 << 127) - 1;
    int p254m1 = (1 << 255) - 1;
    int pflagm1 = (1 << flag_size) - 1;


    while (~ s.is_null()) {
        (x, s_bits, s_refs) = s.slice_compute_data_size(1);
        repeat (s_bits) {
            cur_str &= p254m1;
            cur_str <<= 1;
            cur_str |= s~load_uint(1);
            last_update += 1; 
            if (bits_since_flag < flag_size) {
                bits_since_flag += 1;
            }

            if ((cur_str & pflagm1) == flag) {         
                if (bits_left < last_update - bits_since_flag) {
                    builders~tpush(cur_builder);
                    cur_builder = begin_cell();
                    bits_left = 1023;
                }
                cur_builder~store_uint((cur_str >> bits_since_flag) & ((1 << (last_update - bits_since_flag)) - 1), 
                                        last_update - bits_since_flag);
                bits_left -= (last_update - bits_since_flag);

                if (bits_left < value_size) {
                    builders~tpush(cur_builder);
                    cur_builder = begin_cell();
                    bits_left = 1023;
                }
                cur_builder~store_uint(value, value_size);
                bits_left -= value_size;

                last_update = 0; 
                cur_str = 0;
                bits_since_flag = 0;
            }

            elseif (last_update == uint_size + bits_since_flag) {
                if (bits_left < uint_size) {
                    builders~tpush(cur_builder);
                    cur_builder = begin_cell();
                    bits_left = 1023;
                }
                cur_builder~store_uint((cur_str >> bits_since_flag) & p126m1, uint_size);
                bits_left -= uint_size;

                last_update = bits_since_flag;
            }
        }

        if (s1.is_null()) {
            s = null();
        }
        else {
            s = s1.begin_parse();
            if (~ s.slice_refs_empty?()) {
                s1 = s~load_ref();
            } 
            else {
                s1 = null();
            }
        }
    }

    if (last_update != 0) {
        if (bits_left < last_update) {
            builders~tpush(cur_builder);
            cur_builder = begin_cell();
            bits_left = 1023;
        }
        cur_builder~store_uint(cur_str & ((1 << last_update) - 1), last_update);
    }

    builder tmp2 = cur_builder;
    builder tmp1 = begin_cell();
    int i = builders.tuple_length() - 1;
    while (i >= 0) {
        tmp1 = builders.at(i);
        tmp1 = tmp1.store_ref(tmp2.end_cell());
        tmp2 = tmp1;
        i -= 1;
    }

    return tmp2.end_cell(); 
}

